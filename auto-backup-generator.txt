# Automated backup generator

# High and low watermark for starting generator
define min_stored_power 1000
define max_stored_power 2000

# IC Pins
alias inout_generator d0
alias out_warning_light d1

define station_battery_hash -400115994
define lg_station_battery_hash -1388288459

alias total_sm_batt r0
alias total_lg_batt r1
alias total_stored_power r2
alias generator_state r3

loop:
# check stored battery level
lb total_sm_batt station_battery_hash Charge 1
lb total_lg_batt lg_station_battery_hash Charge 1
add total_stored_power total_sm_batt total_lg_batt
l generator_state inout_generator On

# if the generator is currently on
beqz generator_state gen_off
# if total > max, turn off generator
blt total_stored_power max_stored_power end_loop
s inout_generator On 0
s out_warning_light On 0
j end_loop

gen_off:
# if total < min, turn on generator
bgt total_stored_power min_stored_power end_loop
s inout_generator On 1
s out_warning_light On 1

end_loop:
yield
j loop